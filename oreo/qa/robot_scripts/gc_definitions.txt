*** Settings ***
Library         Selenium2Library
Library         String
Resource        gcbalance_definitions.txt
Resource        checkout_definitions.txt
Resource        demandware_configuration_definitions.txt
Resource        cart_definitions.txt
Resource        common.txt
Resource        common_keywords.txt

*** Variables ***
${GIFTCARDBALANCE_SECTION}  giftcard-tab
${A_EGC}        electronicgc-view
${T_EGC}        Don't Mess With Stamps
${R_1stEGC}     //div[@id='electronic-giftcard-container']//ul[@class='giftcard-options-container']/li[1]/input
${R_1stPGC}     //div[@id='physical-giftcard-container']//ul[@class='giftcard-options-container']/li[1]/input
${I_RECIPIENT}  dwfrm_electronicgiftcard_purchase_recipient
${I_EMAIL}      dwfrm_electronicgiftcard_purchase_recipientEmail
${I_CONFIRMEMAIL}  dwfrm_electronicgiftcard_purchase_confirmRecipientEmail
${A_PGC}        physicalgc-view
${T_PGC}        Never Go Wrong With A Gift Card
${I_QTY}        dwfrm_physicalgiftcard_purchase_quantity
${M_BGC}        Enter Your Gift Card Number and PIN Below
${B_ADD2CART}   AddToBasketButton

*** Keywords ***
Go to Specific Gift Card Page
  [Arguments]  ${tab}  ${expected}
  Wait Until Page Contains Element  ${GIFTCARDBALANCE_SECTION}
  Click Element  ${tab}
  Wait Until Page Contains  ${expected}

Check and Buy Electronic Gift Card
  Go to Specific Gift Card Page  ${A_EGC}  ${T_EGC}
  Wait Until Page Contains Element  ${R_1stEGC}
  ${valueGC} =  Get Attribute Value  ${R_1stEGC}  value
  Click Element  ${R_1stEGC}
  Fill EGC Data
  Add GiftCard to Cart
  Open Cart
  Verify Gift Card  ${valueGC}  TRUE
  Click Checkout Button from Cart
  Continue checkout with Credit Card

Check and Buy Physical Gift Card
  Go to Specific Gift Card Page  ${A_PGC}  ${T_PGC}
  Wait Until Page Contains Element  ${R_1stPGC}
  ${valueGC} =  Get Attribute Value  ${R_1stPGC}  value
  Click Element  ${R_1stPGC}
  Input Text  ${I_QTY}  1
  Select Radio Button  sleeve-type  Custom
  Add GiftCard to Cart
  Open Cart
  Click Checkout Button from Cart
  Continue checkout with Credit Card

Add GiftCard to Cart
  Click Button  ${B_ADD2CART}

Fill EGC Data
  Input Text  ${I_RECIPIENT}  ${validSite_name} ${validSite_lastName}
  Input Text  ${I_EMAIL}  ${validSite_user}
  Input Text  ${I_CONFIRMEMAIL}  ${validSite_user}

Verify Electronic Fields
  Wait Until Page Contains  ${validSite_name} ${validSite_lastName}
  Wait Until Page Contains  ${validSite_user}

Verify Gift Card
  [Arguments]  ${valueGC}  ${flag}
  Wait Until Page Contains  ${valueGC}
  ${length} =  Get Length  ${valueGC}
  @{id} =  Split String To Characters  ${valueGC}
  Run Keyword If  '${length}'=='7'  Wait Until Page Contains  $@{id}[2]@{id}[3].00
  Run Keyword If  '${length}'=='8'  Wait Until Page Contains  $@{id}[2]@{id}[3]@{id}[4].00
  Run Keyword If  '${flag}'=='TRUE'  Verify Electronic Fields
