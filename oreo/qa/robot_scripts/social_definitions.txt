*** Settings ***
Documentation   Elements for Social login and social windows
Library         Selenium2Library
Resource        login_definitions.txt
Resource        logout_definitions.txt
Resource        pdp_declarations.txt

*** Variables ***
${D_FACEBOOK_BUTTON}  //div[@title='Facebook']/div/div
${D_TWITTER_BUTTON}  //div[@title='Twitter']/div/div
${T_GIGYAFBEMAIL}  //*[@id="email"]
${T_GIGYAFBPASSWD}  //*[@id="pass"]
${B_GIGYAFBLOGIN}  //input[@name='login']
${FBHEADER}     Facebook
${T_GIGYATWEMAIL}  //*[@id="username_or_email"]
${T_GIGYATWPASSWD}  //*[@id="password"]
${B_GIGYATWLOGIN}  //input[@id='allow']
${TWHEADER}     Twitter / Authorize an application
${A_GIGYACLOSE}  //a[@id='btnClose']

*** Keywords ***
Verify Windows
  [Arguments]  ${button}  ${header}
  Wait Until Page Contains Element  ${button}
  Click Element  ${button}
  Wait Until Keyword Succeeds  ${sel_timeout}  2 sec  Select Window  title=${header}

Fill Login Social
  [Arguments]  ${emailLocator}  ${passwdLocator}  ${email}  ${password}
  Wait Until Page Contains Element  ${emailLocator}
  Input Text  ${emailLocator}  ${email}
  Input Text  ${passwdLocator}  ${password}

Click Social Button
  [Arguments]  ${network}
  Run Keyword If  '${network}'=='fb'  Click Button  ${B_GIGYAFBLOGIN}  ELSE  Click Button  ${B_GIGYATWLOGIN}

Close Window
  Wait Until Page Contains Element  ${A_GIGYACLOSE}
  Click Element  ${A_GIGYACLOSE}

Close Social Login Window
  [Arguments]  ${network}
  Run Keyword If  '${network}'=='tw'  Close Window
  Select Window

Login Social Network
  [Arguments]  ${network}  ${email}  ${password}
  Click Login
  Run Keyword If  '${network}'=='fb'  Verify Windows  ${D_FACEBOOK_BUTTON}  ${FBHEADER}  ELSE  Verify Windows
  ...  ${D_TWITTER_BUTTON}  ${TWHEADER}
  Run Keyword If  '${network}'=='fb'  Fill Login Social  ${T_GIGYAFBEMAIL}  ${T_GIGYAFBPASSWD}  ${email}  ${password}
  ...  ELSE  Fill Login Social  ${T_GIGYATWEMAIL}  ${T_GIGYATWPASSWD}  ${email}  ${password}
  Click Social Button  ${network}

Verify Social Login and Logout
  [Arguments]  ${network}  ${email}  ${password}
  Login Social Network  ${network}  ${email}  ${password}
  Run Keyword If  ${env_id} != 1  Close Social Login Window  ${network}  ELSE  Select Window
  Verify Login  ${email}  FALSE
  Menu Logout
  Verify Logout

Share Social Network
  [Arguments]  ${network}  ${category}  ${subcategory}
  Select Specific SubCategory  ${category}  ${subcategory}
  Open Random Product From Category Page
  Verify Share Options in PDP  ${network}
  Run Keyword If  '${network}' == "facebook"  Verify Social Window Title  facebook
  Run Keyword If  '${network}' == "twitter"  Verify Social Window Title  Share a link on Twitter
  Run Keyword If  '${network}' == "pinterest"  Verify Social Window Title  Pinterest: Discover and save creative ideas

Verify Social Window Title
  [Arguments]  ${SocialWindowTitle}
  Wait Until Keyword Succeeds  ${sel_timeout}  2 sec  Select Window  ${SocialWindowTitle}
