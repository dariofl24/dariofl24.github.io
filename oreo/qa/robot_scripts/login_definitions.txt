*** Settings ***
Library         Selenium2Library
Resource        site_declarations.txt
Resource        logout_definitions.txt
Resource        common_keywords.txt

*** Variables ***
${I_EMAIL_LOGIN}  //input[contains(@id,'dwfrm_login_username')]
${I_PASSWORD_LOGIN}  //input[@id='dwfrm_login_password']
${A_LOGIN}      //div[@id='login-btn']
${I_KEEPME}     //input[@id='dwfrm_login_rememberme']
${C_REMBERMECOOKIE}  dwcustomer
${LI_ACCOUNT_SETTINGS}  //*[@id="header"]/header/div[3]/ul/li[1]/a
${LI_MYFAMILY}  //*[@id="header"]/header/div[3]/ul/li[4]/a
${ERRORMESSAGE1}  //div[@class='error-line'][1]
${ERRORMESSAGEFIELD1}  //span[@class='error']
${D_ACCOUNTINFO}  //div[@id='account-info-box'][contains(text() ,'

*** Keywords ***
Run if Fails
  Close All Browsers
  Open Browser to Converse Site

Fill Login Data
  [Arguments]  ${email}  ${password}
  Wait Until Page Contains Element  ${I_EMAIL_LOGIN}
  Input Text  ${I_EMAIL_LOGIN}  ${email}
  Input Text  ${I_PASSWORD_LOGIN}  ${password}
  Checkbox Should Not Be Selected  ${I_KEEPME}

Check RememberMe Login
  Select Checkbox  ${I_KEEPME}
  Checkbox Should Be Selected  ${I_KEEPME}

Submit Login Data
  [Arguments]  ${expected}
  Click Element  ${A_LOGIN}
  Wait Until Page Contains Element  ${D_ACCOUNTINFO}${expected}')]

Verify Login Message
  [Arguments]  ${expected}  ${type}
  Click Element  ${A_LOGIN}
  Wait Until Page Contains Element  ${type}
  ${message} =  Get Text  ${type}
  Should Contain  ${message}  ${expected}

Verify Login
  [Arguments]  ${username}  ${cookie}
  Wait Until Page Contains Element  ${A_HIMENU}
  Click Element  ${A_HIMENU}
  Click Element  ${LI_ACCOUNT_SETTINGS}
  Wait Until Page Contains Element  ${S_MYACCOUNTSTGS}
  Location Should Contain  ${U_ACCOUNTURL}
  ${emailP} =  Get Text Value  ${S_PROFILEMAIL}
  Should Be Equal As Strings  ${username}  ${emailP}
  Verify Cookie  ${C_REMBERMECOOKIE}  ${cookie}

Verify Automatic Login after Customer Register
  [Arguments]  ${username}  ${cookie}
  Wait Until Page Contains Element  ${A_HIMENU}
  Click Element  ${A_HIMENU}
  Click Element  ${LI_ACCOUNT_SETTINGS}
  Wait Until Page Contains  ${S_MYACCOUNTSTGS}
  Location Should Contain  ${U_ACCOUNTURL}
  ${emailP} =  Get Text Value  ${S_PROFILEMAIL}
  Should Be Equal As Strings  ${username}  ${emailP}

Login Should Pass When
  [Arguments]  ${username}  ${password}  ${expected}  ${cookie}  ${homeUrl}=home
  Fill Login Data  ${username}  ${password}
  Run Keyword If  "${cookie}" == "TRUE"  Check RememberMe Login
  Submit Login Data  ${expected}
  Verify Login  ${username}  ${cookie}
  Menu Logout
  Verify Logout  ${homeUrl}

Login Should Fail When
  [Arguments]  ${username}  ${password}  ${expected}  ${type}
  Fill Login Data  ${username}  ${password}
  Run Keyword If  '${type}'=='errormessage'  Verify Login Message  ${expected}  ${ERRORMESSAGE1}  ELSE  Verify Login Message
  ...  ${expected}  ${ERRORMESSAGEFIELD1}
  Wait Until Page Contains  ${LOGINSIGNUP}
  Click Login
