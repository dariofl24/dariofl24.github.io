*** Settings ***
Documentation   Elements for account settings pages
Library         Selenium2Library
Resource        logout_definitions.txt
Resource        login_definitions.txt
Resource        register_definitions.txt
Resource        site_declarations.txt

*** Variables ***
${D_TABS_SECUNDARYMENU}  //div[@id='wrapper']
${A_TAB_PROFILE}  //a[@id='profile-view']
${A_TAB_ADDRESSES}  //a[@id='address-view']
${A_TAB_SUSCRIPTIONS}  //a[@id='subscriptions-view']
${UL_SUBMENU_BOX}  //div[@id='account-box visible active']
${CLASS_ACTIVE_TAB}  [contains(@class, 'active')]
${F_PROFILE}    //form[@id='profile-form']
${B_SAVE}       //div[@class='form-row subscribe-button']//button[@name='dwfrm_subscriptions_save']
${T_NAME}       //input[@id='dwfrm_profile_customer_firstname']
${T_LASTNAME}   //input[@id='dwfrm_profile_customer_lastname']
${S_EMAIL}      //span[@id='profileCustomerEmail']
${T_ZIPCODE}    //input[@id='dwfrm_profile_customer_regional_zip']
${R_RADIOGENDER}  //input[@name='dwfrm_profile_customer_gender']
${B_SAVEPROFILE}  //button[@name='dwfrm_profile_confirm']
${T_PROFILETAB}  //a[@id='profile-view']
${L_DELETEACCOUNT}  //div[@id='profileButtonPanel']/div[2]//a[@class='delete-account-link']
${D_WARNING_SECTION}  //div[@id='primary']/div[2]
${U_WARNINGTITLE}  /div/div[1]//h2
${U_WARNINGPOINTS}  /div/div[1]//li
${D_BUTTONSECTION}  /div/div[2]
${B_DELETEACCOUNTBUTTON}  /div/button[2]
${D_INTERESTCONTAINER}  //div[@id='interests-checkbox-group']
${F_SUBSCRIPTIONSFORM}  //form[@id='subscription-form']
${D_INTEREST_GROUP}  //div[@id='interests-checkbox-group']
${B_ADDREMOVELEFT}  //div[@id='primary']/ul/li
${B_ADDREMOVERIGTH}  /div/button[2]
${B_ADDEDITRIGTH}  /div/button[@class='edit-address-button button-fancy']
${F_ADDRRFORM}  //div[@class='converse-tabs standard-content-container']//form[@id='address-form']
${T_ADDRFIELDS_REG}  //input[@id='dwfrm_profile_address_addressFields_regional_
${T_ADDRFIELDS}  //input[@id='dwfrm_profile_address_addressFields_common_
${T_ADDRFIELDS_PREV}  //input[@id='dwfrm_profile_address_
${T_ADDRFIELDSCLOSE}  ']
${T_ADDRNICK}   addressid
${T_ADDRNAME}   firstName
${T_ADDRLASTN}  lastName
${T_ADDRADDR}   address1
${T_ADDRCITY}   city
${T_ADDRZIP}    zip
${T_ADDRPHONE}  phone
${T_ADDRSTATE}  //select[@id='dwfrm_profile_address_addressFields_states_stateCode
${T_ADDRDEFAULT}  //input[@name='dwfrm_profile_address_isdefault
${B_ADDRSAVE}   //button[@class='apply-button
${B_ADDRCANCEL}  //button[@class='cancel cancel-button
${B_ADDRADD}    add-address-button
${D_PROMOTIONS_ERROR_MESSAGE}  //div[@class='error-line'][contains (text(),'An error')]

*** Keywords ***
Start My Profile
  Open Browser to Converse Site
  Login To Site With Valid Data
  Navigate to Account Settings

Open My Profile
  Open Browser to Converse Site
  Login To Site With Valid Data
  Navigate to Account Settings

Account Settings
  [Arguments]  ${Type}  ${name}  ${lastname}  ${zipcode}  ${email}  ${firstnameLabel}
  ...  ${lastnameLabel}  ${zipcodeLabel}
  Verifies Labels are Preset in Account Settings
  Set Profile  Tester  Testing  87452  2
  Check Saved Profile Data  Tester  Testing  ${email}  87452  2
  Set Profile  ${name}  ${lastname}  ${zipcode}  1
  Check Saved Profile Data  ${name}  ${lastname}  ${email}  ${zipcode}  1

Login To Site With Valid Data
  Wait Until Page Contains Element  ${A_LOGIN_MENU}
  Click Element  ${A_LOGIN_MENU}
  Fill Login Data  ${validSite_user}  ${validSite_pass}
  Click Element  ${A_LOGIN}

Navigate to Account Settings
  Wait Until Page Contains Element  ${A_HIMENU}
  Click Element  ${A_HIMENU}
  Wait Until Page Contains Element  ${LI_ACCOUNT_SETTINGS}
  Click Element  ${LI_ACCOUNT_SETTINGS}

Verifies Labels are Preset in Account Settings
  Check Text  1  1  1  ${firstnameLabel}
  Check Text  1  2  1  ${lastnameLabel}
  Check Text  2  1  1  EMAIL ADDRESS
  Check Text  2  2  1  DATE OF BIRTH
  Check Text  3  1  1  ${zipcodeLabel}
  Check Text  3  2  1  GENDER
  Check Text  4  1  1  CHANGE PASSWORD

Check Text
  [Arguments]  ${row}  ${column}  ${id}  ${text}
  Wait Until Page Contains Element  ${F_PROFILE}/div[${row}]/div[${column}]/label/span[${id}]
  Element Should Contain  ${F_PROFILE}/div[${row}]/div[${column}]/label/span[${id}]  ${text}

Check Saved Profile Data
  [Arguments]  ${name}  ${lastname}  ${email}  ${zipcode}  ${gender}
  Wait Until Page Contains Element  ${D_TABS_SECUNDARYMENU}${A_TAB_PROFILE}${CLASS_ACTIVE_TAB}
  Wait Until Page Contains Element  ${D_ACCOUNTINFO}${name} ${lastname}')]
  Textfield Should Contain  ${F_PROFILE}${T_NAME}  ${name}
  Textfield Should Contain  ${F_PROFILE}${T_LASTNAME}  ${lastname}
  Wait Until Page Contains Element  ${F_PROFILE}${T_ZIPCODE}[@value='${zipcode}']
  ${emailP} =  Get Text Value  ${S_PROFILEMAIL}
  Should Be Equal As Strings  ${email}  ${emailP}
  Wait Until Page Contains Element  ${R_RADIOGENDER}[@value='${gender}'][@checked='checked']

Set Profile
  [Arguments]  ${name}  ${lastname}  ${zipcode}  ${gender}
  Wait Until Page Contains Element  ${D_TABS_SECUNDARYMENU}${A_TAB_PROFILE}${CLASS_ACTIVE_TAB}
  Sleep  1
  Wait Until Page Contains Element  ${F_PROFILE}${T_NAME}
  Input Text  ${F_PROFILE}${T_NAME}  ${name}
  Input Text  ${F_PROFILE}${T_LASTNAME}  ${lastname}
  Input Text  ${F_PROFILE}${T_ZIPCODE}  ${zipcode}
  Click Element  ${F_PROFILE}${R_RADIOGENDER}[@value='${gender}']
  Click Element  ${F_PROFILE}${B_SAVEPROFILE}
  Sleep  5
  Navigate to Account Settings

Prepare Subscriptions Test
  Unmark checkbox  1
  Unmark checkbox  2
  Unmark checkbox  3
  Save Subscriptions
  Navigate to Account Settings

Select All Subscriptions
  Prepare Subscriptions Test
  Click Subscriptions
  Mark checkbox  1
  Mark checkbox  2
  Mark checkbox  3
  Save Subscriptions
  Navigate to Account Settings
  Click Subscriptions
  Check selected checkbox  1
  Check selected checkbox  2
  Check selected checkbox  3

Test Specific Subscriptions
  [Arguments]  ${checkrow}  ${check1}  ${check2}
  Prepare Subscriptions Test
  Click Subscriptions
  Mark checkbox  ${checkrow}
  Save Subscriptions
  Navigate to Account Settings
  Click Subscriptions
  Check selected checkbox  ${checkrow}
  Check unselected checkbox  ${check1}
  Check unselected checkbox  ${check2}
  Save Subscriptions
  Page Should Not Contain Element  ${D_PROMOTIONS_ERROR_MESSAGE}

Mark checkbox
  [Arguments]  ${row}
  Select Checkbox  ${D_INTEREST_GROUP}/div[${row}]/div[1]/input[@type='checkbox']

Unmark checkbox
  [Arguments]  ${row}
  Unselect Checkbox  ${D_INTEREST_GROUP}/div[${row}]/div[1]/input[@type='checkbox']

Check selected checkbox
  [Arguments]  ${row}
  Wait Until Page Contains Element  ${D_INTEREST_GROUP}/div[${row}]/div[1]/input[@type='checkbox']
  Checkbox Should Be Selected  ${D_INTEREST_GROUP}/div[${row}]/div[1]/input[@type='checkbox']

Check unselected checkbox
  [Arguments]  ${row}
  #Sleep  2
  Wait Until Page Contains Element  ${D_INTEREST_GROUP}/div[${row}]/div[1]/input[@type='checkbox']
  Checkbox Should Not Be Selected  ${D_INTEREST_GROUP}/div[${row}]/div[1]/input[@type='checkbox']

Save Subscriptions
  Click Button  ${F_SUBSCRIPTIONSFORM}${B_SAVE}

Click Subscriptions
  Wait Until Page Contains Element  ${D_TABS_SECUNDARYMENU}${A_TAB_SUSCRIPTIONS}
  Click Element  ${D_TABS_SECUNDARYMENU}${A_TAB_SUSCRIPTIONS}
  Sleep  3

User creates and deletes account
  [Arguments]  ${user}  ${password}  ${zipcode}  ${message}  ${errortype}
  Register Test Starts
  ${loginname} =  Get Time  epoch
  Fill Register Data  ${loginname}${user}  ${password}  ${zipcode}  1  2  26
  ...  1976
  Submit Register Data
  Navigate to Account Settings
  Deletes Account
  Verifies Deletion Warnings
  Confirm Delete
  Verify Logout
  Click Login
  Login Should Fail When  ${loginname}${user}  ${password}  ${message}  ${errortype}

Deletes Account
  Wait Until Page Contains Element  ${L_DELETEACCOUNT}
  Click Element  ${L_DELETEACCOUNT}
  Verifies Deletion Warnings

Verifies Deletion Warnings
  Wait Until Page Contains Element  ${D_WARNING_SECTION}
  Page Should Contain Element  ${D_WARNING_SECTION}${U_WARNINGTITLE}[contains(text(), 'Your account lets you:')]
  Check Warning  1  Save your designed sneakers
  Check Warning  2  Checkout faster when you shop
  Check Warning  3  Get the latest updates
  Check Warning  4  Easily track your orders

Check Warning
  [Arguments]  ${li}  ${warning}
  Page Should Contain Element  ${D_WARNING_SECTION}${U_WARNINGPOINTS}[${li}][contains (text(), '${warning}')]

Confirm Delete
  Wait Until Page Contains Element  ${D_WARNING_SECTION}${D_BUTTONSECTION}${B_DELETEACCOUNTBUTTON}
  Click Element  ${D_WARNING_SECTION}${D_BUTTONSECTION}${B_DELETEACCOUNTBUTTON}

Add and Modify Address
  [Arguments]  ${city}  ${state}  ${zip}  ${default}=0  ${name}=Robot  ${lastName}=Framework
  ...  ${address1}=123 Happy Testing  ${phone}=333-333-3333
  Fill Address  ${city}  ${state}  ${zip}  1  ${name}  ${lastName}
  ...  ${address1}  ${phone}
  Save Address
  Verify Address  ${city}  ${state}  ${zip}
  Edit Address
  Fill Address  ${city}  ${state}  ${zip}  ${default}  ${name}  ${lastName}
  ...  ${address1}  ${phone}
  Save Address
  Verify Address  ${city}  ${state}  ${zip}
  Edit Address
  Fill Address  Guadalajara  ${state}  ${zip}  1  ${name}  ${lastName}
  ...  ${address1}  ${phone}
  Cancel Address
  Verify Not Exist Address  Guadalajara  IA  44220
  Verify Address  ${city}  ${state}  ${zip}

Add New Address
  [Arguments]  ${city}  ${state}  ${zip}  ${default}=0  ${name}=Robot  ${lastName}=Framework
  ...  ${address1}=123 Happy Testing  ${phone}=333-333-3333
  Fill Address  ${city}  ${state}  ${zip}  ${default}  ${name}  ${lastName}
  ...  ${address1}  ${phone}
  Save Address
  Verify Address  ${city}  ${state}  ${zip}

Clear Address Content
  ${qty} =  Return Address Qty
  Run Keyword If  ${qty}>0  Remove All Addresses  ${qty}

Return Address Qty
  Sleep  4
  ${qty} =  Get Matching Xpath Count  ${B_ADDREMOVELEFT}${B_ADDREMOVERIGTH}
  Log  ${qty}
  [Return]  ${qty}

Remove All Addresses
  [Arguments]  ${qty}
  Log  Removing addreses
  : FOR  ${any}  IN RANGE  ${qty}
  \  Log  ${any} ${qty}
  \  Remove Address

Full Address Content
  ${count} =  Get Matching Xpath Count  ${B_ADDRADD}
  ${qty} =  Return Address Qty
  Run Keyword If  ${count}!=0  Fail  Able to Add more addresses, expected to not
  Run Keyword If  ${qty}!=5  Fail  Expected 5 Addresses saved and the count doesn't match

Remove Address
  Wait Until Page Contains Element  ${B_ADDREMOVELEFT}[1]${B_ADDREMOVERIGTH}
  Click Element  ${B_ADDREMOVELEFT}[1]${B_ADDREMOVERIGTH}
  Sleep  1
  ${legend} =  Confirm Action
  Should Be Equal  ${legend}  Do you want to remove this address?

Fill Address
  [Arguments]  ${city}  ${state}  ${zip}  ${default}  ${name}  ${lastName}
  ...  ${address1}  ${phone}
  Wait Until Keyword Succeeds  ${sel_timeout}  2 sec  Element Should Be Visible  ${F_ADDRRFORM}${T_ADDRFIELDS_PREV}${T_ADDRNICK}${T_ADDRFIELDSCLOSE}
  Input Text  ${F_ADDRRFORM}${T_ADDRFIELDS_PREV}${T_ADDRNICK}${T_ADDRFIELDSCLOSE}  ${city}
  Input Text  ${F_ADDRRFORM}${T_ADDRFIELDS}${T_ADDRNAME}${T_ADDRFIELDSCLOSE}  ${name}
  Input Text  ${F_ADDRRFORM}${T_ADDRFIELDS}${T_ADDRLASTN}${T_ADDRFIELDSCLOSE}  ${lastName}
  Input Text  ${F_ADDRRFORM}${T_ADDRFIELDS}${T_ADDRADDR}${T_ADDRFIELDSCLOSE}  ${address1}
  Input Text  ${F_ADDRRFORM}${T_ADDRFIELDS}${T_ADDRCITY}${T_ADDRFIELDSCLOSE}  ${city}
  Run Keyword If  '@{ENV_LOCALE}[${env_id}]'=='US'  Select From List By Value  ${F_ADDRRFORM}${T_ADDRSTATE}${T_ADDRFIELDSCLOSE}  ${state}
  Input Text  ${F_ADDRRFORM}${T_ADDRFIELDS_REG}${T_ADDRZIP}${T_ADDRFIELDSCLOSE}  ${zip}
  Input Text  ${F_ADDRRFORM}${T_ADDRFIELDS_REG}${T_ADDRPHONE}${T_ADDRFIELDSCLOSE}  ${phone}
  Run Keyword If  '${default}'=='1'  Select Checkbox  ${F_ADDRRFORM}${T_ADDRDEFAULT}${T_ADDRFIELDSCLOSE}

Save Address
  Wait Until Page Contains Element  ${F_ADDRRFORM}${B_ADDRSAVE}${T_ADDRFIELDSCLOSE}
  Click Element  ${F_ADDRRFORM}${B_ADDRSAVE}${T_ADDRFIELDSCLOSE}
  Wait Until Page Contains Element  ${B_ADDREMOVELEFT}[1]

Cancel Address
  Click Element  ${F_ADDRRFORM}${B_ADDRCANCEL}${T_ADDRFIELDSCLOSE}

Edit Address
  Wait Until Page Contains Element  ${B_ADDREMOVELEFT}[1]${B_ADDEDITRIGTH}
  Click Element  ${B_ADDREMOVELEFT}[1]${B_ADDEDITRIGTH}
  Wait Until Page Contains Element  ${F_ADDRRFORM}${B_ADDRSAVE}${T_ADDRFIELDSCLOSE}

Add Address
  Wait Until Page Contains Element  ${B_ADDRADD}
  Click Element  ${B_ADDRADD}
  Wait Until Keyword Succeeds  ${sel_timeout}  2 sec  Element Should Be Visible  ${F_ADDRRFORM}${B_ADDRSAVE}${T_ADDRFIELDSCLOSE}

Click Addresses
  Wait Until Page Contains Element  ${D_TABS_SECUNDARYMENU}${A_TAB_ADDRESSES}
  Click Element  ${D_TABS_SECUNDARYMENU}${A_TAB_ADDRESSES}
  #Wait Until Page Contains Element  //div[@id='add-address-place-holder']

Verify Address
  [Arguments]  ${city}  ${state}  ${zip}
  Wait Until Page Contains  ${city}
  Wait Until Page Contains  ${state}
  Wait Until Page Contains  ${zip}

Verify Not Exist Address
  [Arguments]  ${city}  ${state}  ${zip}
  Page Should Not Contain  ${city}
  Page Should Not Contain  ${state}
  Page Should Not Contain  ${zip}
