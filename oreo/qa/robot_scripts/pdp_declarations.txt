*** Settings ***
Documentation   General Site Declarations and main functions from homepage
Library         Selenium2Library
Resource        checkout_definitions.txt
Resource        common_keywords.txt
Resource        demandware_marketing_definitions.txt
Resource        backorder_definitions.txt

*** Variables ***
${D_PRODUCTCONTENT}  //div[@id='product-content']
${S_PRODUCTCOLOR}  //fieldset//li[@class='attribute'][1]//select[@class='swatches Color']
${S_PRODUCTSIZE}  //fieldset//li[@class='attribute'][2]//select[@class='swatches size']
${B_ADDTOCART}  //button[@id='add-to-cart']
${I_PRODUCTNUMBER}  //div[@class='regular hc']//div[@class='product-attributes']//input[@id='Quantity']
${F_COLLAPSED}  [@style='display: none;']
${D_MINICART_CONTENT}  //div[@class='mini-cart-content']
${F_EXPANDED}   [@style='display: block;']
${S_PRODUCTQUANTITY}  Quantity
${P_PRODUCTNAME}  //div[@id='pdpMain']/div[1]/h1[text()='
${P_PRODUCTPRICE}  //div[@id='pdpMain']/div[1]/div/span[text()='
${P_PRODUCT_IMAGE}  //div[@id='pdpMain']/div[3]/div[1]/div[1]/a/div/img
${D_COLORSWATCHES}  //div[@class='color-swatch']
${L_COLORSWATCHES}  //li
${S_SELECTSIZE}  sizes
${O_SIZEOPTIONS}  /option
${S_SIZES}      //select[@id='sizes']
#PDP Behavior validations
${S_COLOR}      //span[@class='value'][contains(text(),'
${S_SELECTED_SIZE}  //select[@id='sizes']//option[@selected='selected'][contains (text(),'
${S_SHAREICON}  //div[@id='share-links-pdp_gig_containerParent']/a/span[@class='share-icon']
${I_TWITTER_FRAME}  twitter-widget-0
${D_SHAREBUTTON_PDP_FACEBOOK}  //div[@id='share-links-pdp']//div[contains(@class,'gig-button-container-facebook')]//div[contains(text(),'Facebook')]
${D_SHAREBUTTON_PDP_TWITTER}  //div[@id='share-links-pdp']//div[contains(@class,'gig-button-container-twitter')]//iframe
${D_SHAREBUTTON_PDP_PINTEREST}  //div[@id='share-links-pdp']//div[contains(@class,'gig-button-container-pinterest')]//div[contains(text(),'Pinterest')]

*** Keywords ***
Select an Specific Product and Navigate to PDP Page
  Open Browser to Converse Site
  Search Specific Product  ${id_Inlineproduct}
  ${colorid} =  Set Product Color Options Available
  ${productColor} =  Get Product Color  ${colorid}
  Add One Product to Cart from PDP
  Verify Color and Size In Page  ${productColor}  ${InlineSize}
  Click Checkout Button from Cart
  Fill Checkout Info For Color and Size Validations

Set a Product Number
  [Arguments]  ${number}
  #Select From List  ${S_PRODUCTQUANTITY}  ${number}
  Wait Until Page Contains Element  ${I_PRODUCTNUMBER}
  Input Text  ${I_PRODUCTNUMBER}  ${number}
  Press Key  ${I_PRODUCTNUMBER}  \\9
  Click Element  //label[@for='Quantity']

Set Product Color Options Available
  Wait Until Page Contains Element  ${D_COLORSWATCHES}
  ${ColorOptions} =  Get Matching Xpath Count  ${D_COLORSWATCHES}${L_COLORSWATCHES}
  Run Keyword If  ${colorOptions}<=1  Warn About One Color Option
  ${randomsize} =  Evaluate  random.randint(2, ${colorOptions})  random
  ${colorid} =  Convert To String  ${randomsize}
  [Return]  ${colorid}

Get Product Color
  [Arguments]  ${color}
  ${productColor} =  Get Element Attribute  ${D_COLORSWATCHES}${L_COLORSWATCHES}[${color}]//a@title
  Click Element  ${D_COLORSWATCHES}${L_COLORSWATCHES}[${color}]//a
  [Return]  ${productColor}

Select Product Size
  Wait Until Page Contains Element  ${S_SIZES}
  Click Element  ${S_SIZES}/option[contains(@value,'${InlineSize}')]

Warn About One Color Option
  Pass Execution  Mingy color options to verify swatches and sizes

Add a Product to Cart
  Wait Until Page Contains Element  ${B_ADDTOCART}
  ${enabled} =  Get Element Attribute  ${B_ADDTOCART}@disabled
  Run Keyword If  "${enabled}" == "true"  Out of Inventory
  Click Button  ${B_ADDTOCART}

Out of Inventory
  Fail  'The current product doesn't have inventory availability please set inventory allocation > 0'  beforeRGA

Select Product Color
  [Arguments]  ${color}
  Click Element  ${D_PRODUCTCONTENT}${S_PRODUCTCOLOR}
  Click Element  //select//option[contains (text(), '${color}')]

Verify First Available Color Is Selected By Default
  [Arguments]  ${selectedColor}
  Wait Until Page Contains Element  ${S_COLOR}${selectedColor}')]

Display Share Options
  Wait Until Page Contains Element  ${S_SHAREICON}
  Click Element  ${S_SHAREICON}

Open Share Window
  [Arguments]  ${network}
  Wait Until Page Contains Element  ${D_SHAREBUTTON_PDP_FACEBOOK}
  Run Keyword If  '${network}' == "facebook"  Click Element  ${D_SHAREBUTTON_PDP_FACEBOOK}
  Run Keyword If  '${network}' == "twitter"  Open Twitter Iframe
  Run Keyword If  '${network}' == "pinterest"  Click Element  ${D_SHAREBUTTON_PDP_PINTEREST}

Verify Share Options in PDP
  [Arguments]  ${network}
  Display Share Options
  Open Share Window  ${network}

Open Twitter Iframe
  Select Frame  twitter-widget-0
  Click Element  b
